import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

ext.env = System.getenv()

if(env.DRONE_BUILD_NUMBER) { // Drone.io Support
	ext.buildNumber = env.DRONE_BUILD_NUMBER
} else if (env.BUILD_NUMBER) { // Jenkins Support
	ext.buildNumber = env.BUILD_NUMBER
} else {
	ext.buildNumber = config.build_number
}

version = "${config.minecraft_version}-${config.mod_version}.${buildNumber ?: 0}"

group= "com.iconmaster.aec2" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "AetherCraft2"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version

    replaceIn "AetherCraft.java"
    replace "@VERSION@", "${config.mod_version}.${buildNumber ?: 0}"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'mod_version':project.version, 'minecraft_version':project.config.minecraft_version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
	maven {
		name = "iconmaster"
		url = "http://dl.bintray.com/iconmaster5326/maven" 
	}
}